plugins {
    id 'application'
    id "com.google.osdetector" version "1.6.2"
}

dependencies {
    testImplementation 'org.powermock:powermock-easymock-release-full:1.6.4'
    testImplementation 'org.easymock:easymock:3.4'
    testImplementation 'javax.xml.bind:jaxb-api:2.2.11'
}

application {
    mainClassName 'org.zeromq.App'
}

task buildJzmqLib(type: Exec) {
    onlyIf {
        osdetector.os == 'linux'
    }

    doFirst {
        exec { executable './autogen.sh' }
        exec {
            executable './configure'
            environment 'JAVA_HOME', System.properties.'java.home'
            args '--prefix', "$buildDir/bin"
        }
        exec { executable 'make' }
    }

    commandLine 'make', 'install'

    outputs.dir("$buildDir/bin/lib")
}

test {
    dependsOn jar
    doFirst {
        classpath += jar.outputs.files
        if (JavaVersion.current().isJava9Compatible()) {
            jvmArgs '--illegal-access=warn'
            jvmArgs '--add-opens', 'java.base/java.lang=ALL-UNNAMED'
            jvmArgs '--add-opens', 'java.base/java.io=ALL-UNNAMED'
            jvmArgs '--add-opens', 'java.base/java.net=ALL-UNNAMED'
            jvmArgs '--add-opens', 'java.base/java.util=ALL-UNNAMED'
        }
        failFast true
    }
}

jar {
    doFirst {
        if (JavaVersion.current().isJava9Compatible()) {
            manifest {
                attributes('ModuleMainClass': mainClassName)
            }
        } else {
            manifest {
                attributes('Main-Class': mainClassName)
            }
        }
    }

    into("NATIVE/${System.properties.'os.arch'}/$osdetector.os") {
        from(buildJzmqLib)
    }
}
