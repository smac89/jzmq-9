plugins {
    id 'org.kordamp.gradle.project' version '0.27.0' apply false
}

apply plugin: 'org.kordamp.gradle.bom'

allprojects {
    apply plugin: 'org.kordamp.gradle.project'

    group 'org.zeromq'
    version '3.1.1'
}

// POM reference: https://maven.apache.org/pom.html
config {
    release = true
    info {
        description = """The 0MQ lightweight messaging kernel is a library which extends
            > the standard socket interfaces with features traditionally
            > provided by specialised messaging middleware products. 0MQ sockets
            > provide an abstraction of asynchronous message queues, multiple
            > messaging patterns, message filtering (subscriptions), seamless
            > access to multiple transport protocols and more.
            >
            > This package contains the Java Bindings for ZeroMQ.""".stripMargin(">")

        inceptionYear = '2007'
        tags = ['zmq', 'jzmq', 'zeromq', 'Ømq']
        vendor = "zeromq"

        links {
            website = "https://zeromq.org"
        }

        scm {
            url = "https://github.com/zeromq/jzmq"
            connection = "scm:git:git://github.com/zeromq/jzmq.git"
            developerConnection = "scm:git:ssh://git@github.com/zeromq/jzmq.git"
        }

        organization {
            name = "zeromq"
            url = "https://zeromq.org"
        }

        people {
            person {
                id = "zeromq"
                name = "The ØMQ project"
            }
        }

        issueManagement {
            url = "https://github.com/zeromq/jzmq/issues"
        }

        ciManagement {
            system = "Travis CI"
            url = "https://travis-ci.org"
        }

        repositories {
            repository {
                name = 'mavenTmp'
                url  = 'file:///tmp/repo/maven'
            }
        }
    }

    publishing {
        releasesRepository  = 'mavenTmp'
        snapshotsRepository = 'mavenTmp'
    }

    licensing {
        licenses {
            license {
                primary = true
                id = 'LGPL-3.0-or-later'
                name = "LGPLv3+"
            }
        }
    }
}

subprojects {
    apply plugin: 'java'

    repositories {
        mavenCentral()
    }

    config {
        buildInfo {
            skipBuildBy = true
            skipBuildJdk = true
        }
    }

    def moduleName = project.name

    sourceSets {
        java9 {
            java {
                srcDirs 'src/main/java9'
            }
        }
    }

    afterEvaluate {
        dependencies {
            java9Implementation files(sourceSets.main.output.classesDirs) {
                builtBy compileJava
            }
        }

        tasks.withType(JavaCompile) {
            doFirst {
                options.compilerArgs += [
                        '-Xlint:deprecation'
                ]
            }
        }

        compileJava {
            sourceCompatibility = JavaVersion.VERSION_1_7
            targetCompatibility = JavaVersion.VERSION_1_7

            doFirst {
                options.compilerArgs += [
                        '--release', '7'
                ]
            }
        }

        compileJava9Java {
            inputs.property('moduleName', moduleName)
            sourceCompatibility = JavaVersion.VERSION_1_9
            targetCompatibility = JavaVersion.VERSION_1_9

            doFirst {
                options.compilerArgs += [
                        '--module-path', (classpath + compileJava.classpath).asPath,
                        '--release', '9'
                ]
                classpath = files()
            }
        }

        test {
            failFast true
        }

        jar {
            inputs.property('moduleName', moduleName)

            into('META-INF/versions/9') {
                from sourceSets.java9.output
            }

            manifest {
                attributes('Multi-Release': 'true')
                attributes('Automatic-Module-Name': moduleName)
            }
        }

        publishing {
            publications {
                "${project.name - ~/(\w+(?>\.))+/}"(MavenPublication) {
                    from components.java
                    artifact sourceJar
                    artifact javadocJar
                }
            }

            repositories {
                maven {
                    name = 'mavenTmp'
                    url  = 'file:///tmp/repo/maven'
                }
            }
        }

        license {
            // https://github.com/hierynomus/license-gradle-plugin#license-extension
            ignoreFailures = true
        }
    }
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
    gradleVersion = '5.6.2'
}
